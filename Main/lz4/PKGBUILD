# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=lz4
pkgver=1.7.5
pkgrel=1
pkgdesc='Very fast lossless compression algorithm'
arch=('x86_64')
url='https://github.com/Cyan4973/lz4'
license=('GPL2')
groups=('core')
checkdepends=('diffutils')
depends=('glibc'
         'gcc')
source=("lz4-${pkgver}.tar.gz::https://github.com/Cyan4973/lz4/archive/v${pkgver}.tar.gz")

build() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr
}

check() {
  cd "${pkgname}-${pkgver}"
  programs/lz4 /etc/passwd passwd.lz4
  programs/lz4 -d passwd.lz4 passwd
  diff -q /etc/passwd passwd
  rm passwd
}

package() {
  cd "${pkgname}-${pkgver}"
  make install PREFIX=/usr DESTDIR="${pkgdir}"
}

sha256sums=('0190cacd63022ccb86f44fa5041dc6c3804407ad61550ca21c382827319e7e7e')
