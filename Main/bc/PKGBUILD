# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>

pkgname=bc
pkgver=1.07.1
pkgrel=1
pkgdesc="The Bc package contains an arbitrary precision numeric processing language."
arch=('x86_64')
url="http://www.gnu.org/software/${pkgname}/"
license=('GPL')
groups=('core')
makedepends=('bash'
             'binutils'
             'bison'
             'coreutils'
             'gcc'
             'glibc'
             'grep'
             'make'
             'patch')
depends=('readline')
checkdepends=('gawk')
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz")

prepare(){
  cd "${pkgname}-${pkgver}"
  cat > bc/fix-libmath_h << "EOF"
#! /bin/bash
sed -e '1   s/^/{"/' \
    -e     's/$/",/' \
    -e '2,$ s/^/"/'  \
    -e   '$ d'       \
    -i libmath.h

sed -e '$ s/$/0}/' \
    -i libmath.h
EOF
}

build(){
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr             \
              --with-readline           \
              --mandir=/usr/share/man   \
              --infodir=/usr/share/info
  make
}

check(){
  cd "${pkgname}-${pkgver}"
  echo "quit" | ./bc/bc -l Test/checklib.b 2>&1 | tee test.log || true
  # This should have exactly 7 failurs or 10 failures - verify it
  num_fails=`grep "failed" test.log | grep -vc "0$"`
  if [ "${CARCH}" == "x86_64" ]
  then
    [ "${num_fails}" -eq "7" ] || exit 1
  else
    [ "${num_fails}" -eq "10" ] || exit 1
  fi
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

sha256sums=('62adfca89b0a1c0164c2cdca59ca210c1d44c3ffc46daf9931cf4942664cb02a')
